plugins {
    id 'net.ivoa.vo-dml.vodmltools' version '0.5.5'
    id 'java-library'
    id 'checkstyle'
    id "com.github.eerohele.saxon-gradle" version "0.2.1"
    id 'maven-publish'
}


vodml {
    bindingFiles.setFrom(file("src/main/vo-dml/CAOM-2.5.vodml-binding.xml"))
}
group = 'org.opencadc'

version = '2.5.0-SNAPSHOT'

jar {
    enabled = true
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = 'Common Archive Observation Model'
                description = 'a general purpose data model for use as the core data model of an astronomical data centre'

            }
        }
    }
}

defaultTasks 'build', 'xslt'

task initHTML(type: Copy) {
    
    project.mkdir('build/html')
        
    from 'src/main/resources'
    into 'build/html'
    include '**/*.png'
    include '**-profile.html'
}

xslt {
    dependsOn initHTML
    
    stylesheet file('src/main/xsl/vo-dml2html.xsl')
    input file('src/main/resources/CAOM-' + project.version + '-vodml.xml')
    output file('build/html/index.html')
    
    parameters(
        project_name: 'Common Archive Observation Model',
        preamble: '../resources/preamble.html', // relative to stylesheet apparently
        abstract: '../resources/abstract.html',
        status: '../resources/status.html',
        acknowledgment: '../resources/acknowledgment.html',
        graphviz_png: 'CAOM-' + project.version + '.png'
    )
    
}

dependencies {
    implementation 'org.javastro.ivoa.vo-dml:ivoa-base:1.0-SNAPSHOT'
    testImplementation 'org.opencadc:cadc-vodml:[1.0.9,1.1)'

    testImplementation 'junit:junit:4.13'
}

apply from: 'opencadc.gradle'

